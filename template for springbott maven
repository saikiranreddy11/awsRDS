resource "aws_acm_certificate" "app_cert" {
  domain_name               = var.domain_name
  validation_method         = "DNS"
  subject_alternative_names = var.san_names

  lifecycle {
    create_before_destroy = true
  }
}

locals {
  domain_validation_records = {
    for dvo in aws_acm_certificate.app_cert.domain_validation_options :
    dvo.resource_record_name => {
      name   = dvo.resource_record_name
      type   = dvo.resource_record_type
      record = dvo.resource_record_value
    } if dvo.resource_record_name != ""
  }
}

resource "aws_route53_record" "app_cert_validation" {
  for_each = local.domain_validation_records

  zone_id = var.hosted_zone_id
  name    = each.value.name
  type    = each.value.type
  records = [each.value.record]
  ttl     = 60

  lifecycle {
    ignore_changes = [records]
  }

  depends_on = [aws_acm_certificate.app_cert]
}

resource "aws_acm_certificate_validation" "app_cert" {
  certificate_arn         = aws_acm_certificate.app_cert.arn
  validation_record_fqdns = [for record in aws_route53_record.app_cert_validation : record.fqdn]

  depends_on = [aws_route53_record.app_cert_validation]
}
